# Maintainer: David Herrmann <dh.herrmann@gmail.com>

pkgbase=kdbus
pkgname=('kdbus-e531')
pkgver=4.3

_major=`expr $pkgver : '\([0-9]\+\)\.[0-9]\+'`
_minor=`expr $pkgver : '[0-9]\+\.\([0-9]\+\)'`
_nxver="${_major}.$((${_minor} + 1))"
_extramodules="extramodules-$pkgver-e531"

pkgrel=3
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd/kdbus"
makedepends=('xmlto' 'docbook-xsl' 'libxslt' 'kmod' 'inetutils' 'bc' 'git'
             "linux-e531-headers>=$pkgver" "linux-e531-headers<$_nxver")
options=('debug' '!strip')
install=$pkgname.install
source=("git://github.com/systemd/kdbus.git#branch=v$pkgver"
        "$pkgname.install")
md5sums=('SKIP'
         '5771e8bfb8d6c9b833f6cf923ca4f163')

pkgver() {
  cd "$pkgbase"

  #
  # You *MUST* set $pkgver to the kernel version you want to build kdbus for.
  # Upstream provides branches for all kernels since v4.0.
  #
  # As default, we set the version of the current stable arch kernel. However,
  # you can easily adjust this line here to your running kernel version.
  # You can also uncomment the small script to automatically adapt to your
  # *currently* running kernel.
  #
  # Please be aware that if you change the version, you *HAVE* to run:
  #     $ makepkg --nodeps --nobuild
  # ..to update $pkgver of the PKGBUILD correctly. If you don't do this, you
  # will get a dependency warning on 'linux-headers' as pkgver() is called
  # *after* checking $makedepends.
  #

  # fixed version:
  echo 4.3

  # current kernel:
  #echo `expr $(uname -r) : '\([0-9]\+\.[0-9]\+\)'`
}

prepare() {
  cd "$pkgbase"

  # nothing to do?
}

build() {
  cd "$pkgbase"

  make ${MAKEFLAGS} KERNELVER="$(cat /usr/lib/modules/$_extramodules/version)"
}

package_kdbus-e531() {
  pkgdesc="kdbus ipc kernel module"
  license=('GPL2')
  depends=("linux-e531>=$pkgver" "linux-e531<$_nxver")

  cd "$pkgbase"

  install -Dm644 "ipc/kdbus/kdbus.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/kdbus.ko"

  cd $startdir
  cp -f $install ${install}.pkg
  true && install=${install}.pkg
  sed -i "s/EXTRAMODULES=.*/EXTRAMODULES=$_extramodules/" $install
}

# vim: ft=sh syn=sh et
